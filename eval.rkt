#lang typed/racket/base

(require racket/match
         "core-lang.rkt"
         "env.rkt")
(provide
 Ast-eval
 Ast-apply-values)

(: Ast-eval (-> Ast AstEnv CompState Env (U Mark #f) (Values CompState Val)))

(define (Ast-eval ast ast-env state env mark)
  (match ast
    ((App (list (Closure (Fun (list vars ...) body) ast-env*) args ...))
     (unless (= (length vars) (length args))
       (error "Ast-eval: wrong number of arguments" vars args))
     (define-values (#{state* : CompState} #{ast-env** : AstEnv})
       (for/fold ((state state)
                  (ast-env* ast-env*))
                 ((var vars) (arg args))
         (define-values (state* arg*) (Ast-eval arg ast-env state env mark))
         (values state* (cons (list var arg*) ast-env*))))
     (Ast-eval body ast-env** state* env mark))
    ((App (list (PrimOp 'lvalue) arg-ast))
     (define-values (state* arg) (Ast-eval arg-ast ast-env state env mark))
     (match arg
       ((ResolvedId (Sym name))
        (match (Env-ref env name)
          ((ValBinding val) (values state* val))
          (_
           (error "lvalue: the Id must be bound to a value in the compile time environment" name env))))
       (_
        (error "lvalue takes an Id"))))
    ((App (list (PrimOp 'lvalue) _ ...))
     (error "lvalue takes one argument"))
    ((App (list (PrimOp 'lexpand) exp-ast stops-ast))
     (printf "lexpand: ~s\n" ast)
     (define-values (state* stx) (Ast-eval exp-ast ast-env state env mark))
     (define-values (state** stops) (Ast-eval stops-ast ast-env state* env mark))
     (cond ((not (Mark? mark))
            (error "lexpand: not in a macro application"))
           ((not (Stx? stx))
            (error "lexpand: first arg should be the syntax object to expand" stx))
           ((not (Stops? stops))
            (error "lexpand: second arg should be the set (list) of stop ids" stops))
           (else
            (define i-unmarked (Stx-mark stx mark))
            (define-values (state*** o-unmarked)
              ((CompState-expand state**) state** (Env-set-stops env stops) i-unmarked))
            (define o-marked (Stx-mark o-unmarked mark))
            (values state*** o-marked))))
    ((App (list (PrimOp 'lexpand) _ ...))
     (error "lexpand takes two arguments" ast))
    ((App (list (? PrimOp? op) #{args : (Listof Ast)} ...))
     (define-values (state* reverse-arg-vals)
       (for/fold ((state : CompState state)
                  (reverse-arg-vals : (Listof Val) '()))
                 ((arg : Ast args))
         (define-values (#{state* : CompState} #{arg-val : Val})
           (Ast-eval arg ast-env state env mark))
         (values state* (cons arg-val reverse-arg-vals))))
     (Prim-eval op (reverse reverse-arg-vals) state*))
    ((App (list op-ast args ...))
     (match/values (Ast-eval op-ast ast-env state env mark)
       ((state* (? (make-predicate (U Closure PrimOp)) op))
        (Ast-eval (App (cons op args)) ast-env state* env mark))
       ((state* op)
        (error "Ast-eval: operator must be a Closure or PrimOp" op))))
    ((Var name)
     (let loop ((ast-env ast-env))
       (if (pair? ast-env)
           (let ((binding (car ast-env)))
             (if (eq? name (Var-name (car binding)))
                 (values state (cadr binding))
                 (loop (cdr ast-env))))
           (error "Ast-eval: variable not in current environment" name))))
    ((? Fun? fn)
     (values state (Closure fn ast-env)))
    ((? Val? val)
     (values state val))))

(: Prim-eval (-> PrimOp (Listof Val) CompState (Values CompState Val)))

(define (Prim-eval op args state)
  (match* (op args)
    (((PrimOp 'cons) (list elem (Seq elems ...)))
     (values state (list->Seq (cons elem elems))))
    (((PrimOp 'car) (list (Seq elems ...)))
     (values state (car elems)))
    (((PrimOp 'cdr) (list (Seq elems ...)))
     (values state (list->Seq (cdr elems))))
    (((PrimOp 'list-ref) (list (Seq elems ...) (? (make-predicate Integer) index)))
     (values state (list-ref elems index)))
    (((PrimOp 'list) _)
     (values state (list->Seq args)))
    (((PrimOp 'stx-e) (list (Stx val ctx)))
     (values state val))
    (((PrimOp 'mk-stx) (list (? Exp? val) (Stx _ ctx)))
     (values state (Stx val ctx)))
    (((PrimOp 'mk-stx) (list val (Stx _ ctx)))
     (error "mk-stx: bad expression:" val))
    (((PrimOp 'mk-stx) (list (? Exp? val) ctx))
     (error "mk-stx: bad context:" ctx))
    (((PrimOp 'mk-stx) args)
     (error "mk-stx: requires an expression and a context" args))
    (((PrimOp '+) (list (? (make-predicate Integer) x) (? (make-predicate Integer) y)))
     (values state (+ x y)))
    ((_ _)
     (error "Prim-eval bad primitive form" op args))))

(: Ast-apply-values (-> Closure (Listof Val) CompState Env (U Mark #f) (Values CompState Val)))

(define (Ast-apply-values closure args state env mark)
  ;; NOTE: this does not evaluate the operator or the args.
  (match closure
    ((Closure (Fun (list vars ...) body) ast-env)
     (unless (= (length vars) (length args))
       (error "Ast-apply: wrong number of arguments" vars args))
     (define ast-env* : AstEnv
       (for/fold ((ast-env ast-env))
                 ((var vars) (arg args))
         (cons (list var arg) ast-env)))
     (Ast-eval body ast-env* state env mark))))
